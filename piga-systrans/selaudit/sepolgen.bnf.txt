statements	:	statement
			|	statements statement
			|	empty

statement	:	interface
			|	template
			|	obj_perm_set
			|	policy
			|	policy_module_stmt
			|	module_stmt

empty :

policy_module_stmt	:	POLICY_MODULE OPAREN IDENTIFIER COMMA NUMBER CPAREN

interface	:	INTERFACE OPAREN TICK IDENTIFIER SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN

template	:	TEMPLATE OPAREN TICK IDENTIFIER SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN
			|	DEFINE OPAREN TICK IDENTIFIER SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN

define	:	DEFINE OPAREN TICK IDENTIFIER SQUOTE CPAREN

interface_stmts	:	policy
				|	interface_stmts policy
				|	empty

optional_policy	:	OPT_POLICY OPAREN TICK interface_stmts SQUOTE CPAREN
				|	OPT_POLICY OPAREN TICK interface_stmts SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN

tunable_policy	:	TUNABLE_POLICY OPAREN TICK cond_expr SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN
				|	TUNABLE_POLICY OPAREN TICK cond_expr SQUOTE COMMA TICK interface_stmts SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN

ifelse	:	IFELSE OPAREN TICK IDENTIFIER SQUOTE COMMA COMMA TICK IDENTIFIER SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN optional_semi
		|	IFELSE OPAREN TICK IDENTIFIER SQUOTE COMMA TICK IDENTIFIER SQUOTE COMMA TICK interface_stmts SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN optional_semi

ifdef	:	IFDEF OPAREN TICK IDENTIFIER SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN optional_semi
		|	IFNDEF OPAREN TICK IDENTIFIER SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN optional_semi
		|	IFDEF OPAREN TICK IDENTIFIER SQUOTE COMMA TICK interface_stmts SQUOTE COMMA TICK interface_stmts SQUOTE CPAREN optional_semi

interface_call	:	IDENTIFIER OPAREN interface_call_param_list CPAREN
				|	IDENTIFIER OPAREN CPAREN
				|	IDENTIFIER OPAREN interface_call_param_list CPAREN SEMI

interface_call_param	:	IDENTIFIER
						|	IDENTIFIER MINUS IDENTIFIER
						|	nested_id_set
						|	TRUE
						|	FALSE

interface_call_param_list	:	interface_call_param
							|	interface_call_param_list COMMA interface_call_param

obj_perm_set	:	DEFINE OPAREN TICK IDENTIFIER SQUOTE COMMA TICK names SQUOTE CPAREN

policy	:	policy_stmt
		|	optional_policy
		|	tunable_policy
		|	ifdef
		|	ifelse
		|	conditional

policy_stmt	:	gen_require
			|	avrule_def
			|	typerule_def
			|	typeattribute_def
			|	interface_call
			|	role_def
			|	role_allow
			|	type_def
			|	typealias_def
			|	attribute_def
			|	range_transition_def
			|	role_transition_def
			|	bool
			|	define
			|	initial_sid
			|	genfscon
			|	fs_use
			|	portcon
			|	nodecon
			|	netifcon
			|	pirqcon
			|	iomemcon
			|	ioportcon
			|	pcidevicecon

module_stmt	:	MODULE IDENTIFIER NUMBER SEMI

gen_require	:	GEN_REQ OPAREN TICK requires SQUOTE CPAREN
			|	REQUIRE OBRACE requires CBRACE

requires	:	require
			|	requires require
			|	ifdef
			|	requires ifdef

require	:	TYPE comma_list SEMI
		|	ROLE comma_list SEMI
		|	ATTRIBUTE comma_list SEMI
		|	CLASS comma_list SEMI
		|	BOOL comma_list SEMI

security_context	:	IDENTIFIER COLON IDENTIFIER COLON IDENTIFIER
					|	IDENTIFIER COLON IDENTIFIER COLON IDENTIFIER COLON mls_range_def

gen_context	:	GEN_CONTEXT OPAREN security_context COMMA mls_range_def CPAREN

context	:	security_context
		|	gen_context

initial_sid	:	SID IDENTIFIER context

genfscon	:	GENFSCON IDENTIFIER PATH context

fs_use	:	FS_USE_XATTR IDENTIFIER context SEMI
		|	FS_USE_TASK IDENTIFIER context SEMI
		|	FS_USE_TRANS IDENTIFIER context SEMI

portcon	:	PORTCON IDENTIFIER NUMBER context
		|	PORTCON IDENTIFIER NUMBER MINUS NUMBER context

nodecon	:	NODECON NUMBER NUMBER context
		|	NODECON IPV6_ADDR IPV6_ADDR context

netifcon	:	NETIFCON IDENTIFIER context context

pirqcon	:	PIRQCON NUMBER context

iomemcon	:	IOMEMCON NUMBER context
			|	IOMEMCON NUMBER MINUS NUMBER context

ioportcon	:	IOPORTCON NUMBER context
			|	IOPORTCON NUMBER MINUS NUMBER context

pcidevicecon	:	PCIDEVICECON NUMBER context

mls_range_def	:	mls_level_def MINUS mls_level_def
				|	mls_level_def

mls_level_def	:	IDENTIFIER COLON comma_list
				|	IDENTIFIER

type_def	:	TYPE IDENTIFIER COMMA comma_list SEMI
			|	TYPE IDENTIFIER SEMI
			|	TYPE IDENTIFIER ALIAS names SEMI
			|	TYPE IDENTIFIER ALIAS names COMMA comma_list SEMI

attribute_def	:	ATTRIBUTE IDENTIFIER SEMI

typealias_def	:	TYPEALIAS IDENTIFIER ALIAS names SEMI

role_def	:	ROLE IDENTIFIER TYPES comma_list SEMI
			|	ROLE IDENTIFIER SEMI

role_allow	:	ALLOW names names SEMI

avrule_def	:	ALLOW names names COLON names names SEMI
			|	DONTAUDIT names names COLON names names SEMI
			|	AUDITALLOW names names COLON names names SEMI
			|	NEVERALLOW names names COLON names names SEMI

typerule_def	:	TYPE_TRANSITION names names COLON names IDENTIFIER SEMI
				|	TYPE_CHANGE names names COLON names IDENTIFIER SEMI
				|	TYPE_MEMBER names names COLON names IDENTIFIER SEMI

bool	:	BOOL IDENTIFIER TRUE SEMI
		|	BOOL IDENTIFIER FALSE SEMI

conditional	:	IF OPAREN cond_expr CPAREN OBRACE interface_stmts CBRACE
			|	IF OPAREN cond_expr CPAREN OBRACE interface_stmts CBRACE ELSE OBRACE interface_stmts CBRACE

typeattribute_def	:	TYPEATTRIBUTE IDENTIFIER comma_list SEMI

range_transition_def	:	RANGE_TRANSITION names names COLON names mls_range_def SEMI
						|	RANGE_TRANSITION names names names SEMI

role_transition_def	:	ROLE_TRANSITION names names names SEMI

cond_expr	:	IDENTIFIER
			|	EXPL cond_expr
			|	cond_expr AMP AMP cond_expr
			|	cond_expr BAR BAR cond_expr
			|	cond_expr EQUAL EQUAL cond_expr
			|	cond_expr EXPL EQUAL cond_expr

names	:	identifier
		|	nested_id_set
		|	asterisk
		|	TILDE identifier
		|	TILDE nested_id_set
		|	IDENTIFIER MINUS IDENTIFIER

identifier	:	IDENTIFIER

asterisk	:	ASTERISK

nested_id_set	:	OBRACE nested_id_list CBRACE

nested_id_list	:	nested_id_element
				|	nested_id_list nested_id_element

nested_id_element	:	identifier
					|	MINUS IDENTIFIER
					|	nested_id_set

comma_list	:	nested_id_list
			|	comma_list COMMA nested_id_list

optional_semi	:	SEMI
				|	empty
